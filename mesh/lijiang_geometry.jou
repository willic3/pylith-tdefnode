# -*- Python -*- (syntax highlighting)
# ----------------------------------------------------------------------
#
# Charles Williams, GNS Science
#
# ----------------------------------------------------------------------
#
# CUBIT journal file to create box containing a fault and export the
# geometry.
#
# ----------------------------------------------------------------------
# Set units to SI.
# ----------------------------------------------------------------------
reset
${Units('si')}

# ----------------------------------------------------------------------
# Import fault.
# ----------------------------------------------------------------------
import 'geometry/lijiang_cubit.cub'
merge tolerance 400.0
set boolean regularize on

# ----------------------------------------------------------------------
# Create box surrounding/intersecting fault.
# ----------------------------------------------------------------------
group 'fault' add surface all
${xmin=BBox_XMin("group", 2)}
${xmax=BBox_XMax("group", 2)}
${ymin=BBox_YMin("group", 2)}
${ymax=BBox_YMax("group", 2)}
${zmin=BBox_ZMin("group", 2)}
${zmax=BBox_ZMax("group", 2)}
${xmid=0.5*(xmin+xmax)}
${ymid=0.5*(ymin+ymax)}
${zmid=0.5*(zmin+zmax)}
${xlen=xmax-xmin-2.0*km}
${ylen=ymax-ymin-2.0*km}
${zlen=zmax-zmin-2.0*km}

${xdim=xlen+8.0*km}
${ydim=ylen-12.0*km}
${zmove=-0.5*zlen}

brick x {xdim} y {ydim} z {zlen}
${idFaultBox=Id("volume")}
volume {idFaultBox} move x {xmid+2.0*km} y {ymid-1.5*km} z {zmove+0.001}

# ----------------------------------------------------------------------
# Create outer volume.
# ----------------------------------------------------------------------
playback 'outer_geometry.jou'

# ----------------------------------------------------------------------
# Chop outer volume with inner volume.
# ----------------------------------------------------------------------
chop volume {idOuter} with volume {idFaultBox}

# ----------------------------------------------------------------------
# Webcut volume with fault body.
# ----------------------------------------------------------------------
create body surface 1 to 30 sheet
webcut volume 33 with sheet body 1
delete body 1

# ----------------------------------------------------------------------
# Imprint and merge all volumes.
# ----------------------------------------------------------------------
imprint all
merge all

# ----------------------------------------------------------------------
# Export volume.
# ----------------------------------------------------------------------
save as 'lijiang_geometry.cub' overwrite
